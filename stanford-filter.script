//
// Stanford Research IT MIRC-CTP filtering script
// This script allows specific known pixel PHI, so always use the included pixel scrubbing script.
// 

//
// This group contains very specific whitelisted devices. They may or may not have scrubbing rules.
// If a wanted device gets filtered out, they need to be added here 
//
(
  // KONICA 0402 CR -- SCRUBBED
  (Manufacturer.containsIgnoreCase("KONICA") * Modality.startsWithIgnoreCase("CR")
   *ManufacturerModelName.containsIgnoreCase("0402") 
   *(
     ([0018,1020].startsWithIgnoreCase("6.") * Rows.equalsIgnoreCase("2446") * Columns.equalsIgnoreCase("2446"))
    +([0018,1020].startsWithIgnoreCase("2.") * Rows.equalsIgnoreCase("2010") * Columns.equalsIgnoreCase("2446"))
   )
  )

  // MedicaTechUSA KrstyalRad 660 -- SCRUBBED
  +(Manufacturer.containsIgnoreCase("Medicatechusa") * (Modality.startsWithIgnoreCase("CR") + Modality.startsWithIgnoreCase("DX")) 
    *ManufacturerModelName.startsWithIgnoreCase("KrystalRad 660") 
    *[0018,1020].startsWithIgnoreCase("1.")
  )

  // CANON/SHIMADZU AXIOM / CXDI V4/6 -- SCRUBBED
  +((Manufacturer.containsIgnoreCase("Shimadzu") + Manufacturer.containsIgnoreCase("Canon")) * (Modality.startsWithIgnoreCase("CR") + Modality.startsWithIgnoreCase("DX")) 
   *(
    (ManufacturerModelName.startsWithIgnoreCase("AXIOM") + ManufacturerModelName.startsWithIgnoreCase("CXDI")) 
    *([0018,1020].startsWithIgnoreCase("V6") + [0018,1020].startsWithIgnoreCase("V4") + [0018,1020].startsWithIgnoreCase("2."))
   )  
  )

  // Cuattro CloudDR -- SCRUBBED
  +(Manufacturer.startsWithIgnoreCase("Cuattro") * Modality.startsWithIgnoreCase("DX") 
   *ManufacturerModelName.startsWithIgnoreCase("CloudDR") 
   *[0018,1020].startsWithIgnoreCase("3.")  
   *Rows.equalsIgnoreCase("3072") * Columns.equalsIgnoreCase("3072")
  )

  // Medlink Imaging AltoDR -- SCRUBBED
  +(Manufacturer.startsWithIgnoreCase("Medlink Imaging") * Modality.startsWithIgnoreCase("DX")
   *ManufacturerModelName.startsWithIgnoreCase("AltoDR") 
   *[0018,1020].startsWithIgnoreCase("1.") 
   *Rows.equalsIgnoreCase("2922") * Columns.equalsIgnoreCase("1936")
  )


  // GE CT+PET -- SCRUBBED
  +((Manufacturer.startsWithIgnoreCase("GE MEDICAL") + Manufacturer.startsWithIgnoreCase("GEMS")) * (Modality.equalsIgnoreCase("PT") + Modality.equals("OT") + Modality.equals("CT"))
   *(ManufacturerModelName.startsWithIgnoreCase("Discovery") 
     *[0018,1018].startsWithIgnoreCase("Volume Viewer")
     *([0018,1020].startsWithIgnoreCase("5") + [0018,1020].startsWithIgnoreCase("4"))
     *(
       ([0028,0010].containsIgnoreCase("512") * [0028,0011].containsIgnoreCase("512")) 
      +([0028,0010].containsIgnoreCase("512") * [0028,0011].containsIgnoreCase("256"))
     )
   )
  )
 
  // MIMvista standalone -- No scrubbing needed
  +((Manufacturer.equalsIgnoreCase("MIMvista Corp") * Modality.equals("CT"))
    *[0018,1020].equalsIgnoreCase("")
    *Rows.equalsIgnoreCase("512") 
    *Columns.equalsIgnoreCase("512")
  )

  // MIMvista + Manufacturer -- No scrubbing needed
  +((Manufacturer.containsIgnoreCase("/ MIM") * (Modality.equals("CT") + Modality.equals("PT")))
   *ImageType.contains("DERIVED\PRIMARY")
   *[0018,1020].equalsIgnoreCase("")
  )

  // Siemens Biograph 6, Somaris/5 3D, SOMATOM Definition AS -- No scrubbing needed but be careful of dose
  +(Manufacturer.startsWithIgnoreCase("SIEMENS") * (Modality.equalsIgnoreCase("CT") + Modality.equals("PT"))
   *(
     (ManufacturerModelName.containsIgnoreCase("Biograph 6") * [0018,1020].containsIgnoreCase("syngo CT 2006"))
    +(ManufacturerModelName.containsIgnoreCase("Somaris/5 3D") * [0018,1020].containsIgnoreCase("VA45A-W")) 
    +(ManufacturerModelName.containsIgnoreCase("SOMATOM Definition AS") * [0018,1020].containsIgnoreCase("syngo MI.PET/CT 2012"))
    +(SecondaryCaptureDeviceManufacturerModelName.containsIgnoreCase("SOMATOM Definition AS") * DerivationDescription.equals("MEDCOM RESAMPLED"))
    +(ManufacturerModelName.startsWithIgnoreCase("Emotion"))
    +(ManufacturerModelName.containsIgnoreCase("Sensation"))
   )
  )
  
  // Philips PT/CT fusion
  +(Manufacturer.startsWithIgnoreCase("Philips") * Modality.equals("PT") * 
	  ManufacturerModelName.startsWithIgnoreCase("Guardian") *
	  [0018,1018].startsWithIgnoreCase("EBW") *
    [0018,1020].startsWithIgnoreCase("9.3.1") *
    (
      (Rows.equals("839") * Columns.equals("638"))
      +(Rows.equals("918") * Columns.equals("453"))
      +(Rows.equals("918") * Columns.equals("546"))
      +(Rows.equals("907") * Columns.equals("638"))
    )
  )

  // Segami Mirage (WB MAC P600) -- NM
  +(Manufacturer.equalsIgnoreCase("Segami") 
    *Modality.equals("NM")
	  *ManufacturerModelName.equalsIgnoreCase("Mirage")
    *( 
      (Rows.equals("192") * Columns.equals("192"))
     +(Rows.equals("512") * Columns.equals("512"))
    ) 
  )
  
  // Ceretom dose reports do NOT contain PHI pixels and should be anonymized
  +(Manufacturer.equalsIgnoreCase("Neurologica") 
    *Modality.equals("CT")
	  *ManufacturerModelName.equalsIgnoreCase("CereTom")
    *Rows.equals("512") * Columns.equals("512")
    *ImageType.equals("ORIGINAL\PRIMARY\DOSE")
  )  
  
  // Mammography
  
  // Almost all mammo manufacturers need their own whitelist since the many are DERIVED\PRIMARY
  +(Modality.equals("MG")
    *(
      (Manufacturer.equalsIgnoreCase("CARESTREAM") * ManufacturerModelName.equalsIgnoreCase("CLASSIC CR"))
     +(Manufacturer.containsIgnoreCase("Hologic") * ManufacturerModelName.containsIgnoreCase("Selenia"))
     +(Manufacturer.containsIgnoreCase("Lorad") * ManufacturerModelName.containsIgnoreCase("Selenia"))
     +(Manufacturer.equalsIgnoreCase("GE MEDICAL SYSTEMS") * ManufacturerModelName.containsIgnoreCase("Senograph"))
     +(Manufacturer.equalsIgnoreCase("GE MEDICAL SYSTEMS") * ManufacturerModelName.containsIgnoreCase("ADS_"))
     +(Manufacturer.containsIgnoreCase("MPTronic") * ManufacturerModelName.containsIgnoreCase("Senograph"))
     +(Manufacturer.containsIgnoreCase("FUJI") * ManufacturerModelName.containsIgnoreCase("Clearview CSm"))
     +(Manufacturer.containsIgnoreCase("FUJI") * ManufacturerModelName.containsIgnoreCase("FDR"))
     +(Manufacturer.containsIgnoreCase("Fischer") * ManufacturerModelName.containsIgnoreCase("SenoScan"))
     +(Manufacturer.containsIgnoreCase("IMS") * ManufacturerModelName.containsIgnoreCase("GIOTTO"))
     +(Manufacturer.containsIgnoreCase("KODAK") * ManufacturerModelName.equalsIgnoreCase("ELITE CR"))
     +(Manufacturer.containsIgnoreCase("KODAK") * ManufacturerModelName.equalsIgnoreCase("CLASSIC CR"))
     +(Manufacturer.containsIgnoreCase("SIEMENS") * ManufacturerModelName.containsIgnoreCase("Mammomat"))
     +(Manufacturer.containsIgnoreCase("TMSC") * ManufacturerModelName.equalsIgnoreCase("MGU-1000D"))
    )
    *!(
     // For mammo we can't just filter on DERIVED. So we avoid SECONDARY except for a few manufacturers.
     (ImageType.containsIgnoreCase("SECONDARY")
      *!(
         (Manufacturer.containsIgnoreCase("Hologic") * ManufacturerModelName.containsIgnoreCase("Selenia"))
         +(Manufacturer.containsIgnoreCase("Lorad") * ManufacturerModelName.containsIgnoreCase("Selenia"))
         +(Manufacturer.containsIgnoreCase("TMSC") * ManufacturerModelName.equalsIgnoreCase("MGU-1000D"))
      )
     )
     +ImageType.containsIgnoreCase("SCREEN")
     +ImageType.equals("")
     +BurnedInAnnotation.equalsIgnoreCase("YES")
    )
  )
  
)

//
// If not whitelisted above, then the image must pass the gauntlet below
//
+!(

  // Unsupported modalities using substring, as some manufacturers use "MG/PR" or "SR/US"
	 //Modality.containsIgnoreCase("MG")
	 //Modality.containsIgnoreCase("MA")
	Modality.containsIgnoreCase("RF")
	+Modality.containsIgnoreCase("US")
	+Modality.containsIgnoreCase("XA")

  // Unsupported modalities, exact match
	+Modality.equalsIgnoreCase("AN")
	+Modality.equalsIgnoreCase("AS")
	+Modality.equalsIgnoreCase("BD")
	+Modality.equalsIgnoreCase("BM")
	+Modality.equalsIgnoreCase("BMA")
	+Modality.equalsIgnoreCase("BMD")
	+Modality.equalsIgnoreCase("CF")
	+Modality.equalsIgnoreCase("CV")
	+Modality.equalsIgnoreCase("DE")
	+Modality.equalsIgnoreCase("DEXA")
	+Modality.equalsIgnoreCase("DF")
	+Modality.equalsIgnoreCase("DG")
	+Modality.equalsIgnoreCase("DMA")
	+Modality.equalsIgnoreCase("DOC")
	+Modality.equalsIgnoreCase("EC")
	+Modality.equalsIgnoreCase("ECG")
	+Modality.equalsIgnoreCase("ES")
	+Modality.equalsIgnoreCase("FILM_CT")
	+Modality.equalsIgnoreCase("FL")
	+Modality.equalsIgnoreCase("HC")
	+Modality.equalsIgnoreCase("HD")
	+Modality.equalsIgnoreCase("IO")
	+Modality.equalsIgnoreCase("IR")
	+Modality.equalsIgnoreCase("KO")
	+Modality.equalsIgnoreCase("PN")
	+Modality.equalsIgnoreCase("PR")
	+Modality.equalsIgnoreCase("RAW")
	+Modality.equalsIgnoreCase("REPORT")
	+Modality.equalsIgnoreCase("REQUEST")
	+Modality.equalsIgnoreCase("RG")
	+Modality.equalsIgnoreCase("RTDOSE")
	+Modality.equalsIgnoreCase("RTIMAGE")
	+Modality.equalsIgnoreCase("RTPLAN")
	+Modality.equalsIgnoreCase("RTSTRUCT")
	+Modality.equalsIgnoreCase("SC")
	+Modality.equalsIgnoreCase("SD")
	+Modality.equalsIgnoreCase("SPL")
	+Modality.equalsIgnoreCase("SR")
	+Modality.equalsIgnoreCase("ST")
	+Modality.equalsIgnoreCase("TG")
	+Modality.equalsIgnoreCase("UNQ")
	+Modality.equalsIgnoreCase("XD")  
	
  // Encapsulated PDF
  +[0008,0016].equalsIgnoreCase("1.2.840.10008.5.1.4.1.1.104.1") 

  // Conversion type is not empty
  +!ConversionType.equalsIgnoreCase("")
  
  // the DiCOM box has no ImageType and ComputedRadiographyImageStorage SOPClassUID 
  // So we can only go by the model name. These are video captures.
  +ManufacturerModelName.containsIgnoreCase("the DiCOM box")

  // No Vidar film scanners
  +Manufacturer.containsIgnoreCase("vidar")
  +ManufacturerModelName.containsIgnoreCase("vidar")
  
  // iCAD mammo scanners.. model name is blank so can't be more specific
  +Manufacturer.containsIgnoreCase("icad")
  
  // Presentation state and SR are omitted, for now
  +SOPClassUID.startsWith("1.2.840.10008.5.1.4.1.1.11.") // Soft-copy presentation states
  +SOPClassUID.startsWith("1.2.840.10008.5.1.4.1.1.8") // SR + KO

  // Bad-actor PACS which embed scanned documents as ImageType ORIGINAL and no other hints 
  // other than a high series number 
  +(Manufacturer.containsIgnoreCase("INFINITT") * SeriesNumber.matches("[1-9]\d{3,}"))

  // Biopsy/pathology are omitted for now
  +Manufacturer.containsIgnoreCase("Bioptics")
  +ManufacturerModelName.containsIgnoreCase("Biovision")
  +Manufacturer.containsIgnoreCase("Faxitron")
  +Manufacturer.containsIgnoreCase("KUBTEC")
  +(Manufacturer.containsIgnoreCase("Hologic") * ManufacturerModelName.containsIgnoreCase("Trident"))
  
  // 
  // Sorry, no DERIVED or SECONDARY or NULL Image Types. These must be whitelisted by device.
  // 
  +SOPClassUID.containsIgnoreCase("1.2.840.10008.5.1.4.1.1.7") // Secondary capture
  +ImageType.containsIgnoreCase("DERIVED") 
  +ImageType.containsIgnoreCase("SECONDARY") 
  +ImageType.equals("") 
  +BurnedInAnnotation.equalsIgnoreCase("YES")


)
